Const NET_DLL_FILE  = "cbNetwork.dll"
// JATothrim:n muokkaama nopeampi cbNetwork.cb
//vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
// Muistipalojen käsittely.
// Kirjaston sisäiseen käyttöön.
//vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
Global gNetMemBlock As Integer, gNetMemBlockOffset As Integer

Function _Net_InitMemBlock(_size = 0)
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(_size + 4)
    gNetMemBlockOffset = 4
EndFunction

Function _Net_DeleteMemBlock()
    DeleteMEMBlock gNetMemBlock
    gNetMemBlockOffset = 0
    gNetMemBlock = 0
EndFunction

Function _Net_CallDll(_method As String)
    PokeInt gNetMemBlock, 0, MEMBlockSize(gNetMemBlock)
    CallDLL NET_DLL_FILE, _method, gNetMemBlock
    gNetMemBlockOffset = 0
EndFunction

Function _Net_MemBlockResize(_length)
    If gNetMemBlockOffset + _length > MEMBlockSize(gNetMemBlock) Then
        ResizeMEMBlock gNetMemBlock, gNetMemBlockOffset + _length
    EndIf
EndFunction

Function _Net_PutInt(_value As integer)
    _Net_MemBlockResize(4)
    PokeInt gNetMemBlock, gNetMemBlockOffset, _value
    gNetMemBlockOffset + 4
EndFunction

Function _Net_GetInt()
    Dim lRetVal As integer
    lRetVal = PeekInt(gNetMemBlock, gNetMemBlockOffset)
    gNetMemBlockOffset + 4
    Return lRetVal
EndFunction

Function _Net_PutShort(_value As Short)
    _Net_MemBlockResize(2)
    PokeShort gNetMemBlock, gNetMemBlockOffset, _value
    gNetMemBlockOffset + 2
EndFunction

Function _Net_GetShort()
    Dim lRetVal As integer
    lRetVal = PeekShort(gNetMemBlock, gNetMemBlockOffset)
    If lRetVal > 32768 Then lRetVal = lRetVal - 65536
    gNetMemBlockOffset + 2
    Return lRetVal
EndFunction

Function _Net_GetUShort()
    Dim lRetVal As Short
    lRetVal = PeekShort(gNetMemBlock, gNetMemBlockOffset)
    gNetMemBlockOffset + 2
    Return lRetVal
EndFunction

Function _Net_PutFloat(_value As Float)
    _Net_MemBlockResize(4)
    PokeFloat gNetMemBlock, gNetMemBlockOffset, _value
    gNetMemBlockOffset + 4
EndFunction

Function _Net_GetFloat#()
    Dim lRetVal As Float
    lRetVal = PeekFloat(gNetMemBlock, gNetMemBlockOffset)
    gNetMemBlockOffset + 4
    Return lRetVal
EndFunction

Function _Net_PutByte(_value As Byte)
    _Net_MemBlockResize(1)
    PokeByte gNetMemBlock, gNetMemBlockOffset, _value
    gNetMemBlockOffset + 1
EndFunction

Function _Net_GetByte()
    Dim lRetVal As Byte
    lRetVal = PeekByte(gNetMemBlock, gNetMemBlockOffset)
    gNetMemBlockOffset + 1
    Return lRetVal
EndFunction

Function _Net_PutString(_value As String)
    Dim lStrLen As Integer, i As Integer
    lStrLen = Len(_value)
    _Net_MemBlockResize(lStrLen + 4)
    PokeInt gNetMemBlock, gNetMemBlockOffset, lStrLen
    gNetMemBlockOffset + 4
    For i = 1 To lStrLen
        PokeByte gNetMemBlock, gNetMemBlockOffset, Asc(Mid(_value, i, 1))
        gNetMemBlockOffset + 1
    Next i
EndFunction

Function _Net_GetString$()
    Dim lStrLen As Integer, i As Integer
    Dim lRetVal As String
    Dim c As Byte
    lStrLen = PeekInt(gNetMemBlock, gNetMemBlockOffset)
    gNetMemBlockOffset + 4
    lRetVal = ""
    For i = 1 To lStrLen
        c = PeekByte(gNetMemBlock, gNetMemBlockOffset)
        gNetMemBlockOffset + 1
        If c > 0 Then lRetVal = lRetVal + Chr(c)
    Next i
    Return lRetVal
EndFunction
//^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
// Muistipalojen käsittely.
//^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

//==============================================================================
// API_DLL_Version
//==============================================================================
Global gNetDllVersion As Float
gNetDllVersion = Net_DLL_Version()
If gNetDllVersion = 0 Then
    MakeError "File cbNetwork.dll not be found!"
EndIf
If gNetDllVersion <> 1.0 Then
    MakeError "Incorrect library version (cbNetwork.dll)!"+Chr(10)+Chr(13)+"DLL CODE:"+gNetDllVersion
EndIf

Function Net_DLL_Version#()
    Dim lRetVal As Float
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(8)
    PokeInt gNetMemBlock, 0, 8
    PokeFloat gNetMemBlock, 4, 0
    CallDLL NET_DLL_FILE, "_Version", gNetMemBlock
    lRetVal = PeekFloat(gNetMemBlock, 0)
    DeleteMEMBlock gNetMemBlock
    gNetMemBlockOffset = 0
    gNetMemBlock = 0
    Return lRetVal
EndFunction
//------------------------------------------------------------------------------
// API_DLL_Version
//------------------------------------------------------------------------------

Global HTTP_ContentType As String, HTTP_ContentLength As integer
Global Net_ClientId As String

Function InitServer(_port, _timeout = 5000)
    Dim lRetVal As Integer
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(12)
    PokeInt gNetMemBlock, 0, 12
    PokeInt gNetMemBlock, 4, _port
    PokeInt gNetMemBlock, 8, _timeout
    CallDLL NET_DLL_FILE, "_InitServer", gNetMemBlock
    lRetVal = PeekInt(gNetMemBlock, 0)
    DeleteMEMBlock gNetMemBlock
    gNetMemBlockOffset = 0
    gNetMemBlock = 0
    Return lRetVal
EndFunction

Function CloseServer()
    Dim lRetVal As Integer
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(8)
    PokeInt gNetMemBlock, 0, 8
    CallDLL NET_DLL_FILE, "_CloseServer", gNetMemBlock
    lRetVal = PeekInt(gNetMemBlock, 0)
    DeleteMEMBlock gNetMemBlock
    gNetMemBlockOffset = 0
    gNetMemBlock = 0
    Return lRetVal
EndFunction

Function InitClient(_host As String, _port, _timeout = 5000)
    Dim lRetVal As Integer
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(12 + Len(_host))
    gNetMemBlockOffset = 4
    _Net_PutString(_host)	//MEMBlockresizen poisto!
    PokeInt gNetMemBlock, gNetMemBlockOffset, _port
    PokeInt gNetMemBlock, gNetMemBlockOffset + 4, _timeout
    PokeInt gNetMemBlock, 0, MEMBlockSize(gNetMemBlock)
    CallDLL NET_DLL_FILE, "_InitClient", gNetMemBlock
    lRetVal = PeekInt(gNetMemBlock, 0)
    DeleteMEMBlock gNetMemBlock
    gNetMemBlockOffset = 0
    gNetMemBlock = 0
    Return lRetVal
    WaitKey
EndFunction

Function ClientSend()
    PokeInt gNetMemBlock, 0, MEMBlockSize(gNetMemBlock)
    CallDLL NET_DLL_FILE, "_ClientSend", gNetMemBlock
    gNetMemBlockOffset = 4
    Return PeekInt(gNetMemBlock, 0)
EndFunction

Function ClientSendBack()
    PokeInt gNetMemBlock, 0, MEMBlockSize(gNetMemBlock)
    CallDLL NET_DLL_FILE, "_ClientSendBack", gNetMemBlock
    gNetMemBlockOffset = 4
    Return PeekInt(gNetMemBlock, 0)
EndFunction

Function ClientState()
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(8)
    PokeInt gNetMemBlock, 0, 8
    CallDLL NET_DLL_FILE, "_ClientState", gNetMemBlock
    gNetMemBlockOffset = 4
    Return PeekInt(gNetMemBlock, 0)
EndFunction

Function ClientRead()
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(4100)
    PokeInt gNetMemBlock, 0, 4100
    CallDLL NET_DLL_FILE, "_ClientRead", gNetMemBlock
    gNetMemBlockOffset = 4
    Return PeekInt(gNetMemBlock, 0)
EndFunction

Function GetNetErrorString$(_msg As integer)
    Dim sRetVal As String
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(264)
    PokeInt gNetMemBlock, 0, 264
    PokeInt gNetMemBlock, 4, _msg
    CallDLL NET_DLL_FILE, "_GetErrorString", gNetMemBlock
    gNetMemBlockOffset = 0
    sRetVal = _Net_GetString()
    DeleteMEMBlock gNetMemBlock
    gNetMemBlockOffset = 0
    gNetMemBlock = 0
    Return sRetVal
EndFunction

Function ServerSend()
    PokeInt gNetMemBlock, 0, MEMBlockSize(gNetMemBlock)
    CallDLL NET_DLL_FILE, "_ServerSend", gNetMemBlock
    gNetMemBlockOffset = 4
    Return PeekInt(gNetMemBlock, 0)
EndFunction

Function ServerRead()
    Dim nRetVal As Integer
    If gNetMemBlock Then DeleteMEMBlock gNetMemBlock
    gNetMemBlock = MakeMEMBlock(4100)
    PokeInt gNetMemBlock, 0, 4100
    CallDLL NET_DLL_FILE, "_ServerRead", gNetMemBlock
    nRetVal = PeekInt(gNetMemBlock, 0)
    gNetMemBlockOffset = 4
    Net_ClientId = _Net_GetString() + ":" + PeekInt(gNetMemBlock, gNetMemBlockOffset)
    gNetMemBlockOffset + 4
    Return nRetVal
EndFunction

Function HTTPGet(_url As String, _background As Byte = 0)
    Dim nRetVal As Integer
    _Net_InitMemBlock()
    _Net_PutString(_url)
    PokeByte gNetMemBlock, gNetMemBlockOffset, _background
    _Net_CallDll("_HTTPGet")
    nRetVal = PeekInt(gNetMemBlock, gNetMemBlockOffset)
    HTTP_ContentLength = PeekInt(gNetMemBlock, gNetMemBlockOffset + 4)
    gNetMemBlockOffset + 8
    HTTP_ContentType = _Net_GetString()
    
    _Net_DeleteMemBlock()
    Return nRetVal
EndFunction

Function HTTPDownloadState()
    _Net_InitMemBlock()
    _Net_CallDll("_HTTPDownloadState")
    nRetVal = _Net_GetInt()
    _Net_DeleteMemBlock()
    Return nRetVal
EndFunction

Function HTTPSaveContent(_file$)
    _Net_InitMemBlock()
    _Net_putString(_file)
    _Net_CallDll("_HTTPSaveContent")
    _Net_DeleteMemBlock()
EndFunction

Function HTTPLoadContent()
    _Net_InitMemBlock(HTTP_ContentLength)
    _Net_CallDll("_HTTPLoadContent")
EndFunction

Function HTTPLoadContentImage()
    _Net_InitMemBlock(256)
    _Net_putInt(0)
    _Net_CallDll("_HTTPLoadContentImage")
    nWidth = _Net_GetInt()
    nHeight = _Net_GetInt()
    _Net_DeleteMemBlock()
    _Net_InitMemBlock((nWidth * nHeight) * 4)
    _Net_putInt(1)
    _Net_CallDll("_HTTPLoadContentImage")
    iRetImage = MakeImage(nWidth, nHeight)
    Lock Image(iRetImage)
    For nY = 0 To nHeight - 1
        For nX = 0 To nWidth - 1
            PutPixel2 nX, nY, _Net_GetInt(), Image(iRetImage)
        Next nX
    Next nY
    Unlock Image(iRetImage)
    _Net_DeleteMemBlock()
    Return iRetImage
EndFunction

