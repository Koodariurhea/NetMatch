//********************************************************************************
// Glossary.cb
//
// Ohjelman sanasto
//********************************************************************************


//--------------------------------------------------------------------------------
// Luodaan kokoelma sanaston sanoille
//--------------------------------------------------------------------------------
Type GLOSSARY
    Field key   As String   // Sanan tunnus
    Field lang  As String   // Kieli
    Field label As String   // Teksti
EndType

// Mit‰ kaikkia kieli‰ sanastossa on
Global gLanguages$
gLanguages = ""

//--------------------------------------------------------------------------------
// Luetaan sanasto tiedostosta
//--------------------------------------------------------------------------------

If IsDirectory("glossary") = False Then
    // Puuttuu glossary, pakotetaan p‰ivitys.
    gForceUpdate = True
    updMsgTxt$ = GetText("update_missing_file", "glossary")
    If MsgBox(updMsgTxt, "NetMatch", MSG_INFO) <> 13 Then
        End
    EndIf
Else
    ChDir "glossary"
    // K‰yd‰‰n l‰pi kaikki tiedostot kansiossa
    StartSearch
        Repeat
            file$ = FindFile()
            If file$ = "" Then
                Exit // Kaikki k‰yty l‰pi
            EndIf
            
            If Right( file$, 4 ) = ".gls" Then
                lang$ = Left( file$, Len( file$ ) - 4 )
    
                If lang$ <> "" Then
                    If gLanguages <> "" Then gLanguages = gLanguages + " "
                    gLanguages = gLanguages + Upper( lang )
                EndIf
    
                f=OpenToRead(file$)
                    While Not EOF(f)
                        row$ = GetWord2( ReadLine(f), 1, "#" )
                        key$ = Trim(GetWord2( row$, 1, "=" ))
                        label$ = Trim(Mid( row$, InStr( row$, "=" )+1 ))
                        
                        // Lis‰t‰‰n kokoelmaan
                        If key <> "" And lang <> "" And label <> "" Then
                            gls.GLOSSARY = New(GLOSSARY)
                            gls\key = key
                            gls\lang = lang
                            gls\label = label
                        EndIf
                    Wend
                CloseFile f
            EndIf 
        Forever
    EndSearch
    ChDir ".."
    // Tarkistetaan viel‰ ett‰ ainakin yksi kieli on olemassa
    If gLanguages = "" Then
        // Puuttuu glossary, pakotetaan p‰ivitys.
        gForceUpdate = True
        updMsgTxt$ = GetText("update_missing_file", "glossary")
        If MsgBox(updMsgTxt, "NetMatch", MSG_INFO) <> 13 Then
            End
        EndIf
    EndIf
EndIf

// Valittu kieli
Global gLanguage$
// Oletuksena englanti. Jos englantia ei ole, valitaan ensimm‰inen
If InStr( gLanguages, "EN" ) Then
    gLanguage = "EN"
Else
    gLanguage = GetWord2( gLanguages, 1 )
EndIf

//================================================================================
// Palautetaan kielikoodin mukainen sana
//================================================================================
Function GetText$(_key$, _param1$ = "", _param2$ = "")
    lang$ = gLanguage
    
    For gls.GLOSSARY = Each GLOSSARY
        If Upper(gls\key) = Upper(_key) And Upper(gls\lang) = Upper(lang) Then
            ret$ = gls\label
            If _param1$ <> "" Then ret$ = Replace( gls\label, "%1", _param1 )
            If _param2$ <> "" Then ret$ = Replace( gls\label, "%2", _param2 )
            Return ret$
        EndIf
    Next gls
    If gForceUpdate = True Then //Jos glossaryja ei lˆydy, niin pakotetaan p‰ivitys, jolloin k‰ytet‰‰n n‰it‰.
        If _key = "update_missing_file" Then Return "You are missing %1!|NetMatch can't be played without it.|Press Enter to download all the latest game files."
        If _key = "update_failed"  Then Return "Update failed!"
        If _key = "update_restart" Then Return "Restarting NetMatch"
        If _key = "doing_update"   Then Return "Updating NetMatch..."
    EndIf
    Return ""
EndFunction


//================================================================================
// Asetetaan k‰ytett‰v‰ kieli.
// Jos parametri‰ ei v‰litet‰ niin vaihdetaan seuraavaan kieleen.
//================================================================================
Function SetLanguage(_lang$ = "")
    _lang = Upper(_lang)
    // Vaihdetaan seuraava kieli
    langs = CountWords(gLanguages)
    If _lang = "" Then
        // Kuinka monta kielt‰ sanastossa on
        For i = 1 To langs
            // T‰m‰ on nyt valittuna oleva kieli
            If GetWord2(gLanguages, i) = gLanguage Then
                If i = langs Then 
                    // Jos ollaan viimeisess‰ niin valitaan ensimm‰inen
                    gLanguage = GetWord2(gLanguages, 1)
                    Return True
                Else
                    // muuten valitaan seuraava
                    gLanguage = GetWord2(gLanguages, i + 1)
                    Return True
                EndIf
            EndIf
        Next i
    EndIf
    // Joku kielikoodi on v‰litetty funktiolle tai sitten nykyinen kieli oli joku
    // jota ei sanastossa ole!!
    // K‰yd‰‰n kaikki kielet l‰pi
    For i = 1 To langs
        // Onko kieli listalla
        If GetWord2(gLanguages, i) = _lang Then
            gLanguage = _lang
            Return True
        EndIf
    Next i
    // Parametrin‰ ollut kieli ei kuulu valikoimaan joten valitaan ensimm‰inen
    gLanguage = GetWord2(gLanguages, 1)
EndFunction
